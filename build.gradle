plugins {
    id 'signing'
    id 'java-library'
    id 'maven-publish'
}

group = 'io.github.osipaandr'
version = '1.0.0'

repositories {
    mavenCentral()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'ru-numbers-by-words'
                description = 'Library for converting numbers to words in Russian'
                url = 'https://github.com/osipaandr/ru-numbers-by-words'
                inceptionYear = '2023'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'osipaandr'
                        name = 'Andrey Osipenko'
                        email = 'osipaandr@gmail.com'
                    }
                }
                scm {
                    url = 'https://github.com/osipaandr/ru-numbers-by-words'
                    connection = 'scm:https://github.com/osipaandr/ru-numbers-by-words'
                    developerConnection = 'scm:git:ssh://github.com:osipaandr/ru-numbers-by-words.git'
                }
            }

        }
    }

    repositories {
        maven {
            name = 'sonatype'

            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = project.version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username = project.findProperty("mavenCentralUsername") ?: System.getenv("MAVEN_USERNAME")
                password = project.findProperty("mavenCentralPassword") ?: System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

signing {
    sign publishing.publications.mavenJava
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

task printCredentials {
    doLast {
        println "Username: " + (project.findProperty("mavenCentralUsername") ?: "не определено")
        println "Password: " + (project.findProperty("mavenCentralPassword") ?: "не определено")
    }
}