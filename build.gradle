plugins {
    id 'signing'
    id 'java-library'
    id 'maven-publish'
}

group = 'com.github.osipaandr'
version = '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                url = 'https://github.com/osipaandr/ru-numbers-by-words'
                inceptionYear = '2023'

                licenses {
                    license {
                        name = 'Unlicensed'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'osipaandr'
                        name = 'Andrey Osipenko'
                        email = 'osipaandr@gmail.com'
                    }
                }
                scm {
                    url = 'https://github.com/osipaandr/ru-numbers-by-words'
                    connection = 'scm:https://github.com/osipaandr/ru-numbers-by-words'
                }
            }

        }
    }

    repositories {
        maven {
            name = 'sonatype'

            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = project.version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username = project.findProperty("mavenCentralUsername")
                password = project.findProperty("mavenCentralPassword")
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}
